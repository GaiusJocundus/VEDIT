			;****************************************
			;*					*
			;*		V E D I T		*
			;*					*
			;****************************************
			;
			; Copyright (C) 1984 by CompuView Products, Inc.
			;			1955 Pauline Blvd.
			;			Ann Arbor, MI 48103
			;
			;	Written By: Theodore Green
			;
			;	Last Change: Ted - Nov. 18, 1984
			;
			;
			;********************************************************
			;*							*
			;*	CRT Terminal Dependent Routines and Tables	*
			;*							*
			;********************************************************
			;
			; Copyright (C) 1984 by CompuView Products, Inc.
			;			1955 Pauline Blvd.
			;			Ann Arbor, MI 48103
			;
			;	Written by: Theodore J. Green
			;
			;	Last Change: Ted - Aug. 28, 1984
			;
			; OUTCHR - Output character in Reg. C.
			;	   Return: B, DE restored, HL destroyed.
			;
019B			VISCHR:
019B	79		OUTCHR:	MOV	A,C		;Get char. in A.
019C	4F		OUTCHA:	MOV	C,A		;Save char in C
019D	FE80			CPI	080H		;Is Bit 7 set?
019F	DAAA01			JRC	OUTCHS		;No, process normally
01A2	3A8543			LDA	BIT7AL		;Get EP parameter value
01A5	E602			ANI	2		;Is Bit 7 allowed on output?
01A7	C2C601			JRNZ	OUTCH3		;Yes, send unmodified character
			;
			;	If Bit 7 is set, use reverse video control sequences
			;
01AA	79		OUTCHS:	MOV	A,C		;Get the character.
01AB	E680			ANI	80H		;Look at top bit of char.
01AD	213042			LXI	H,REVVFL	;HL-> reverse video flag (80H if on)
01B0	BE			CMP	M		;Is high bit same as reverse video flag?
01B1	CAC201			JRZ	OUTCH2		;Yes, it will be displayed correctly.
			;
01B4	77			MOV	M,A		;No, set new value of REVVFL
01B5	B7			ORA	A		;Is character in normal video?
01B6	F2BF01			JP	OUTCH1		;Yes, change to normal video.
			;
01B9	CD1703			CALL	CRTVDR		;Enable reverse video on CRT.
01BC	C3C201			JMPR	OUTCH2		;Now send the character out.
			;
01BF	CD1D03		OUTCH1:	CALL	CRTVDN		;Enable normal video on CRT.
			;
01C2	79		OUTCH2:	MOV	A,C		;Get the character back.
01C3	E67F			ANI	7FH		;Strip any high bit.
01C5	4F			MOV	C,A		;Put character in C.
			;
01C6	CDD801		OUTCH3:	CALL	CRTCKK		;Poll for keyboard char. every 30th.
01C9	213242			LXI	H,VRTHOR	;HL-> virtual CRT pos.
01CC	34			INR	M		;Account for CRT moving cursor to the right.
01CD	C5		OUTSAV:	PUSH	B		;Save count in B
01CE	D5			PUSH	D		;Save DE
01CF	CD7205			CALL	CONOUT		;Send char. in reg. C to CRT
01D2	CD2F04			CALL	CHKKYB		;Poll for keyboard char.
01D5	D1			POP	D		;Restore
01D6	C1			POP	B		;Restore count in B
01D7	C9			RET
			;
			; CRTCKK - Polls and buffers any keyboard character after
			;	   every 30 bytes are sent to CRT.
			;
01D8	218442		CRTCKK:	LXI	H,CCOUNT	;HL-> slow poll counter
01DB	34			INR	M		;Increment counter.
01DC	7E			MOV	A,M		;Get the count.
01DD	FE1E			CPI	30		;Reached critical point?
01DF	D8			RC			;No, return.
01E0	3600			MVI	M,00		;Yes, reset count.
01E2	C33004			JMP	CHKKEY		;Poll and buffer any keyboard char.
			;
			; CRTSET - Address the cursor.
			;	   Enter with L = vertical row #.  Top row = 1.
			;	   H = horizontal column #.  Left column = 0.
			;
01E5	C5		CRTSET:	PUSH	B		;Save char. in C.
01E6	D5			PUSH	D
01E7	223342			SHLD	CRTVER		;Save as simulated cursor pos.
01EA	7D			MOV	A,L		;Get row #
01EB	323142			STA	VRTVER		;Save as virtual row #
01EE	EB			XCHG			;Save pos. in DE.
01EF	1D			DCR	E		;Adjust so that top left is 0,0.
01F0	213B03			LXI	H,ADDLED	;HL-> cursor lead in table.
01F3	CD5302			CALL	CRTCRL		;Send cursor address lead in.
01F6	215003			LXI	H,ADDOFF	;HL-> cursor address info.
01F9	7E			MOV	A,M		;Get column or line first switch.
01FA	E601			ANI	1		;Is the line # sent first?
01FC	CA0202			JRZ	CRTSE1		;Yes, D & E are set.
01FF	7A			MOV	A,D		;No, switch D & E.
0200	53			MOV	D,E
0201	5F			MOV	E,A
0202	23		CRTSE1:	INX	H		;HL-> first byte offset.
0203	7B			MOV	A,E		;Get first addr. coordinate.
0204	86			ADD	M		;Add the offset.
0205	E5			PUSH	H		;Save HL-> ADDOFF.
0206	CD1F02			CALL	WRTADD		;Send address to CRT, convert if necessary.
0209	214203			LXI	H,ADDMID	;HL-> middle char. table.
020C	CD5302			CALL	CRTCRL		;Send any chars between first and second byte.
020F	E1			POP	H		;Restore HL.
0210	23			INX	H		;HL-> second byte offset.
0211	7A			MOV	A,D		;Get second addr. coordinate.
0212	86			ADD	M		;Add the offset.
0213	CD1F02			CALL	WRTADD		;Send address to CRT, convert if necessary.
			;
0216	214903			LXI	H,ADDEND	;HL-> cursor address end table.
0219	CD5302			CALL	CRTCRL		;Send any chars to end sequence.
021C	D1			POP	D
021D	C1			POP	B		;Restore char in C.
021E	C9			RET
			;
			; WRTADD - Send the cursor address in Reg. A to CRT and convert
			;	   to Ascii if necessary.  HL and BC clobbered.
			;
							;{CRTSET}
021F	4F		WRTADD:	MOV	C,A		;Save char. in C.
0220	215003			LXI	H,ADDOFF	;HL-> cursor address info.
0223	7E			MOV	A,M		;Get Binary or Ascii address switch.
0224	E680			ANI	80H		;Is address to be converted to Ascii?
0226	CACD01			JRZ	OUTSAV		;No, send address in C to CRT.
			;
0229	79			MOV	A,C		;Yes, get address in A.
022A	013130			LXI	B,03031H	;Get B = '0' is blanking flag, C = '1'.
							;If your CRT needs leading zeroes, let B = 2F hex.
022D	FE64			CPI	100		;Is pos. > 99?
022F	DA3A02			JRC	WRTAD1		;No, branch around
0232	D664			SUI	100		;Yes, subtract the 100
0234	05			DCR	B		;Clear the blanking flag
0235	F5			PUSHA			;Save count
0236	CDCD01			CALL	OUTSAV		;Send the '1'
0239	F1			POPA			;Restore count
			;
023A	0E30		WRTAD1:	MVI	C,'0'		;Init tens counter to '0'
023C	0C		WRTAD2:	INR	C		;Tens = tens + 1
023D	D60A			SUI	10		;Subtract ten
023F	D23C02			JRNC	WRTAD2		;Branch until underflow
			;
0242	C63A			ADI	'0'+10		;Restore from underflow and make Ascii
0244	0D			DCR	C		;Restore from underflow
0245	F5			PUSHA			;Save Ones digit
0246	79			MOV	A,C		;Get the tens digit
0247	B8			CMP	B		;Is tens count zero, and to be blanked?
0248	CA4E02			JRZ	WRTAD4		;Yes, don't send zero to CRT
024B	CDCD01			CALL	OUTSAV		;Send the tens count to CRT
			;
024E	F1		WRTAD4:	POPA			;Restore Ones digit
024F	4F			MOV	C,A		;Put digit in C
0250	C3CD01			JMP	OUTSAV		;Send the digit to the CRT. (Zero not blanked)
			;
			; CRTCRL - Send escape sequence to CRT and perform any necessary delay.
			;	   Enter: HL-> escape sequence table, consisting of escape
			;	   sequence count byte, followed by the bytes making up the
			;	   sequence, followed by any delay in milliseconds.
			;
0253	7E		CRTCRL:	MOV	A,M		;Get the char. count.
0254	B7			ORA	A		;Test for initial zero.
0255	C8			RZ			;Yes, return now.
0256	C5			PUSH	B		;Save BC
0257	47			MOV	B,A		;Put the count in B.
0258	23		CRTCR1:	INX	H		;HL-> escape char.
0259	4E			MOV	C,M		;Get the next char
025A	E5			PUSH	H		;Save PTR
025B	CD2F04			CALL	CHKKYB		;Poll keyboard
025E	CDCD01			CALL	OUTSAV		;Send to CRT
0261	E1			POP	H		;HL-> next char
0262	05			DJNZ	CRTCR1		;Continue sending bytes.
0266	C1			POP	B		;Restore BC
0267	23			INX	H		;HL-> delay byte
0268	7E			MOV	A,M		;Get the delay in milliseconds
0269	B7			ORA	A		;Is there a delay?
026A	C8			RZ			;No, return now
026B	C31838			JMP	DELAY		;Yes, do a delay
			;
			;
			;	Clear Screen Routines.
			;
			; CLRSCR - Clear entire screen.  First try to use Clear Screen sequence.
			;	   A count of 080H means the machine is emulating a terminal.  In
			;	   this case it is more efficient to not perform a clear. Instead,
			;	   an EOL will be sent as each new screen line is written.
			;
026E	3A5303		CLRSCR:	LDA	ESCCLR		;Get count for Clear sequence
0271	B7			ORA	A		;Is this function available?
0272	F8			RM			;80H means emulation is taking place
0273	CA8A02			JRZ	CLRSC2		;Use EOS if Clear screen is not available
							;Note that A = 00
0276	CD1736			CALL	STBRFL		;Set flag to write new status line
0279	215303			LXI	H,ESCCLR	;HL-> escape char. table
027C	CD5302			CALL	CRTCRL		;Perform the clear
027F	AF			XRA	A		;Get a zero
0280	322342			STA	USEDLN		;No lines are in use
0283	210000		CLRCUR:	LXI	H,0000		;Get two zeros
0286	223142			SHLD	VRTVER		;Force CRT cursor set
0289	C9			RET
			;
			;
			; CLRSC2 - Clear rest of screen from begin of next line # in Reg. A.
			;
							;{UPDSCR}
028A	212342		CLRSC2:	LXI	H,USEDLN	;HL-> # screen lines not clear.
028D	BE			CMP	M		;Is screen below last used line clear?
028E	D28302			JRNC	CLRCUR		;Yes, return.
0291	3C			INR	A		;No, get line # of first line to clear.
0292	6F			MOV	L,A		;Get line # for begin of EOS.
0293	2600			MVI	H,00		;Start at left margin.
0295	CDE501			CALL	CRTSET		;Address the cursor.
							;Erase rest of the screen.
			;
			; CRTEOS - Clear to end of Screen from cursor position.
			;
0298	CD1736		CRTEOS:	CALL	STBRFL		;Set flag to write new status line
029B	3A3342			LDA	CRTVER		;Get line # from which to clear.
029E	47			MOV	B,A		;Save number.
029F	215A03			LXI	H,ESCEOS	;HL-> escape char. table.
02A2	7E			MOV	A,M		;Get count for EOS sequence.
02A3	B7			ORA	A		;Does CRT have EOS sequence?
02A4	78			MOV	A,B		;Get back line # for performing EOS.
02A5	CAAE02			JRZ	CTEOS1		;No, simulate EOS with multiple EOLs.
02A8	322342			STA	USEDLN		;Yes, update # used lines.
02AB	C35302			JMPR	CRTCRL		;Perform the screen clear.
			;
			;	Simulate an EOS using multiple EOLs.
			;
02AE	3D		CTEOS1:	DCR	A		;Adjust for CMP below.
02AF	212342			LXI	H,USEDLN	;HL-> # screen lines not clear.
02B2	BE			CMP	M		;Is screen below LINCNT clear?
02B3	D28302			JRNC	CLRCUR		;Yes, return, force cursor set.
			;
02B6	3C			INR	A		;A = original line # for EOS.
02B7	F5			PUSH	PSW		;Save line # for EOS.
02B8	7E			MOV	A,M		;No, get last used line #.
02B9	6F			MOV	L,A		;Set to line to be erased.
02BA	2600			MVI	H,00		;Set to column zero.
02BC	CDE501			CALL	CRTSET		;Position the cursor.
02BF	CDCA02			CALL	CRTEOL		;Erase the line.
02C2	212342			LXI	H,USEDLN	;HL-> # screen lines not clear.
02C5	35			DCR	M		;Account for line just erased.
02C6	F1			POP	PSW		;Restore last used line #.
02C7	C3AE02			JMPR	CTEOS1		;Continue.
			;
			; CRTEOL - Clear to end of Line from cursor position.
			;
02CA	213342		CRTEOL:	LXI	H,CRTVER	;Get current cursor line #.
02CD	3A2342			LDA	USEDLN		;Get # screen lines used.
02D0	BE			CMP	M		;Is rest of current line clear?
02D1	D8			RC			;Yes, return.
							;{WTSLFW}
02D2	216103		CREOL0:	LXI	H,ESCEOL	;HL-> escape char. table.
02D5	7E			MOV	A,M		;Get count for EOL sequence.
02D6	B7			ORA	A		;Does CRT have EOL sequence?
02D7	C25302			JNZ	CRTCRL		;Yes, perform the line clear.
			;
			;	Simulate EOL by sending out spaces to end of line.
			;
02DA	3A9943			LDA	LINELN		;No, get display line length.
02DD	213442			LXI	H,CRTHOR	;HL-> horiz. cursor pos.
02E0	96			SUB	M		;Compute # pos. to clear.
02E1	47			MOV	B,A		;Put count in B.
02E2	0E20			MVI	C,20H		;Clear with spaces.
02E4	CAEE02		CREOL1:	JRZ	CREOL2		;Branch when count is zero.
02E7	CDCD01			CALL	OUTSAV		;Send a space to the CRT.
02EA	05			DCR	B		;Decrement count.
02EB	C3E402			JMPR	CREOL1		;Continue.
			;
02EE	2A3342		CREOL2:	LHLD	CRTVER		;Get desired cursor pos.
02F1	C3E501			JMP	CRTSET		;Reset the cursor.
			;
			; REVOFF - Make sure reverse video CRT mode is off.
			;
							;{WRTLIN}
02F4	213042		REVOFF:	LXI	H,REVVFL	;HL-> reverse video flag
02F7	7E			MOV	A,M		;Get flag
02F8	B7			ORA	A		;Is the reverse video flag set?
02F9	C8			RZ			;No, return.
02FA	3600			MVI	M,00		;Yes, clear flag.
02FC	C31D03			JMPR	CRTVDN		;;Turn reverse video off.
			;
			;
			; CRTINS, CRTDEL - Insert and delete a line on the CRT.
			;	Note: Some CRTs need to be a special mode for Insert and Delete.
			;
02FF	216803		CRTINS:	LXI	H,ESCINS	;HL-> escape char. table.
0302	C35302			JMP	CRTCRL		;Insert a line.
			;
0305	216F03		CRTDEL:	LXI	H,ESCDEL	;HL-> escape char. table.
0308	C35302			JMP	CRTCRL		;Delete a line.
			;
			; CRTFSL, CRTBSL - Perform forward and backward scroll on CRT.
			;
030B	217603		CRTFSL:	LXI	H,ESCFSL	;HL-> escape char. table.
030E	C35302			JMP	CRTCRL		;Scroll forward.
			;
0311	217D03		CRTBSL:	LXI	H,ESCBSL	;HL-> escape char. table.
0314	C35302			JMP	CRTCRL		;Scroll backward.
			;
			; CRTVDR, CRTVDN - Enable and disable reverse video characters.
			;
0317	218403		CRTVDR:	LXI	H,ESCVDR	;HL-> escape char. table.
031A	C35302			JMP	CRTCRL		;Enable reverse video mode.
			;
031D	218B03		CRTVDN:	LXI	H,ESCVDN	;HL-> escape char. table.
0320	C35302			JMP	CRTCRL		;Disable reverse video mode.
			;
			; CRTSTE, CRTSTD - Enable and disable the 25th status line.
			;
0323	219203		CRTSTE:	LXI	H,ESCSTE	;HL-> escape char. table.
0326	C35302			JMP	CRTCRL		;Enable status line.
			;
0329	219903		CRTSTD:	LXI	H,ESCSTD	;HL-> escape char. table.
032C	C35302			JMP	CRTCRL		;Disable status line.
			;
			; CRTENT, CRTEXT - Setup and Reset the CRT for Visual mode editing.
			;
032F	21A003		CRTENT:	LXI	H,ESCENT	;HL-> escape char. table.
0332	C35302			JMP	CRTCRL		;Setup CRT on Visual mode entry.
			;
0335	21A703		CRTEXT:	LXI	H,ESCEXT	;HL-> escape char. table.
0338	C35302			JMP	CRTCRL		;Reset CRT on visual mode exit.
			;
			;
			;	These are the terminal dependent Escape sequence tables.
			;	They are set during customization by selecting a CRT in the menu.
			;
			; ADDLED, ADDMID and ADDEND are for Cursor Addressing.
			;
033B	02		ADDLED:	DB	2		;Count for Cursor Lead In.
033C	1B			DB	01BH		;First byte of sequence.
033D	59			DB	059H		;Second byte.
033E	00000000		DB	00,00,00,00	;Spare bytes.
			;
0342	00		ADDMID:	DB	0		;Count for chars between X & Y.
0343	00			DB	00		;Note: some require a "," between the X & Y.
0344	00			DB	00
0345	00000000		DB	00,00,00,00	;Spare bytes.
			;
0349	00		ADDEND:	DB	0		;Count for terminate chars.
034A	00			DB	00
034B	00			DB	00
034C	00000000		DB	00,00,00,00	;Spare bytes.
			;
			; ADDOFF - Table for Cursor addressing information.  Specifies whether
			;	   the address is sent in binary or Ascii and whether the row
			;	   or column byte is sent first, and the offset to add to the
			;	   row and column addresses.
			;
0350	00		ADDOFF:	DB	0		;Bit 0 : 0 = ROW first then COLUMN
							;	 1 = COLUMN first then ROW
							;Bit 7 : 0 = Address in Binary
							;	 1 = Address in Ascii
0351	20			DB	020H		;First offset
0352	20			DB	020H		;Second offset.
			;
			;	Escape sequence table for other functions.
			;
0353	00		ESCCLR:	DB	00		;Count for CLEAR sequence.
0354	00			DB	00H		;First byte.
0355	00			DB	00H		;Second byte.
0356	00000000		DB	00,00,00,00	;Spare bytes.
			;
035A	02		ESCEOS:	DB	2		;Count for EOS sequence.
035B	1B			DB	01BH
035C	4A			DB	04AH
035D	00000000		DB	00,00,00,00	;Spare bytes.
			;
0361	02		ESCEOL:	DB	2		;Count for EOL sequence.
0362	1B			DB	01BH
0363	4B			DB	04BH
0364	00000000		DB	00,00,00,00	;Spare bytes.
			;
0368	02		ESCINS:	DB	2		;Count for INSERT LINE sequence.
0369	1B			DB	01BH
036A	4C			DB	04CH
036B	00000000		DB	00,00,00,00	;Spare bytes.
			;
036F	02		ESCDEL:	DB	2		;Count for DELETE line sequence.
0370	1B			DB	01BH
0371	4D			DB	04DH
0372	00000000		DB	00,00,00,00	;Spare bytes.
			;
0376	01		ESCFSL:	DB	01		;Count for FORWARD SCROLL sequence.
0377	0A			DB	0AH
0378	00			DB	00H
0379	00000000		DB	00,00,00,00	;Spare bytes.
			;
037D	02		ESCBSL:	DB	2		;Count for BACKWARD SCROLL sequence.
037E	1B			DB	01BH		;The "Insert Line" should work too.
037F	4C			DB	04CH
0380	00000000		DB	00,00,00,00	;Spare bytes.
			;
0384	01		ESCVDR:	DB	01		;Count for BEGIN REVERSE VIDEO sequence.
0385	F2			DB	0F2H
0386	00			DB	00H
0387	00000000		DB	00,00,00,00	;Spare bytes.
			;
038B	01		ESCVDN:	DB	01		;Count for END REVERSE VIDEO sequence.
038C	F2			DB	0F2H
038D	00			DB	00H
038E	00000000		DB	00,00,00,00	;Spare bytes.
			;
0392	00		ESCSTE:	DB	00		;Count for ENABLE STATUS LINE sequence.
0393	00			DB	00H
0394	00			DB	00H
0395	00000000		DB	00,00,00,00	;Spare bytes.
			;
0399	00		ESCSTD:	DB	00		;Count for DISABLE STATUS LINE sequence.
039A	00			DB	00H
039B	00			DB	00H
039C	00000000		DB	00,00,00,00	;Spare bytes.
			;
03A0	00		ESCENT:	DB	00		;Count for ENTER VISUAL MODE sequence.
03A1	00			DB	00H
03A2	00			DB	00H
03A3	00000000		DB	00,00,00,00	;Spare bytes.
			;
03A7	00		ESCEXT:	DB	00		;Count for EXIT VISUAL MODE sequence.
03A8	00			DB	00H
03A9	00			DB	00H
03AA	00000000		DB	00,00,00,00	;Spare bytes.
			;
			;
03AE				DS	40H		;User Patch space.
			;
			;
			;	These are the alternate Console I/O entry points when direct
			;	I/O calls in MP/M are used.  Note that although BDOS CALL 6
			;	is used, these routines only work for MP/M since CP/M 2.2 and
			;	MP/M are quite different in this regard.  Enough patch space
			;	is provided for conversion to CP/M 2.2, or for special user
			;	interfacing.
			;
			; SETIO - Set up internal copy of Console I/O vectors.
			;	  Normally use BIOS, unless MPM - then use BDOS #6
			;
0517	CD1C0B		SETIO:	CALL	MPMCHK		;;Is MP/M running?
051A	212C05			LXI	H,MPMTBL	;For MPM use MPM I/O routines
051D	C22605			JRNZ	SETCP1		;Yes, merge in below
			;
0520	2A0100			LHLD	BASE + 0001	;HL-> BIOS
0523	23			INX$	H
0524	23			INX$	H
0525	23			INX$	H		;HL-> BIOS Console Status
0526	116C05		SETCP1:	LXI	D,CONST		;DE-> Internal I/O jump vectors
0529	C3813A			JMP	MOVE12		;Set I/O vectors (12 bytes) and return
			;
			;
052C	C33805		MPMTBL:	JMP	MPMST		;This table is copied to the BIOS jump
052F	C34505			JMP	MPMIN		;vectors below for the MPM version.
0532	C35805			JMP	MPMOUT
0535	C36205			JMP	MPMLST
			;
0538	0E06		MPMST:	MVI	C,CONSIO	;Perform direct console I/O.
053A	1EFE			MVI	E,0FEH		;FE denotes console status.
053C	C30500			JMP	BDOS		;Get input status.
053F				DS	6		;Patch space for STA TEMPCH, RET
			;
0545	0E06		MPMIN:	MVI	C,CONSIO	;No, perform direct console input.
0547	1EFF			MVI	E,0FFH		;FF denotes console input.
0549	C30500			JMP	BDOS		;Get console char. when available.
054C				DS	12		;Patch space.
			;
0558	59		MPMOUT:	MOV	E,C		;Put the character in E.
0559	0E06			MVI	C,CONSIO	;Perform direct console I/O.
055B	C30500			JMP	BDOS		;Send char. to console through CPM.
055E				DS	4		;Patch space.
			;
0562	59		MPMLST:	MOV	E,C		;Put the character in E.
0563	0E05			MVI	C,5		;Perform BDOS listing.
0565	C30500			JMP	BDOS		;Send char. to listing through CPM.
0568				DS	4		;Patch space.
			;
			;
			; Internal copy of BIOS I/O jump vectors.
			;
056C	C30000		CONST:	JMP	00		;Console status.
056F	C30000		CONINA:	JMP	00		;Get console character.
0572	C30000		CONOUT:	JMP	00		;Send console character.
0575	C30000		LSTOUT:	JMP	00		;Send listing character.

